cargs = ['-DG_LOG_DOMAIN="FuPluginAta"']

install_data([
  'ata.quirk',
  ],
  install_dir: join_paths(datadir, 'fwupd', 'quirks.d')
)

install_data(['ata.conf'],
  install_dir:  join_paths(sysconfdir, 'fwupd')
)

shared_module('fu_plugin_ata',
  fu_hash,
  sources : [
    'fu-plugin-ata.c',
    'fu-ata-device.c',
  ],
  include_directories : [
    root_incdir,
    fwupd_incdir,
    fwupdplugin_incdir,
  ],
  install : true,
  install_dir: plugin_dir,
  c_args : [
    cargs,
    '-DLOCALSTATEDIR="' + localstatedir + '"',
  ],
  link_with : [
    fwupd,
    fwupdplugin,
  ],
  dependencies : [
    plugin_deps,
  ],
)

if get_option('tests')
  testdatadirs = environment()
  testdatadirs.set('G_TEST_SRCDIR', meson.current_source_dir())
  testdatadirs.set('G_TEST_BUILDDIR', meson.current_build_dir())
  e = executable(
    'ata-self-test',
    fu_hash,
    sources : [
      'fu-self-test.c',
      'fu-ata-device.c',
    ],
    include_directories : [
      root_incdir,
      fwupd_incdir,
      fwupdplugin_incdir,
    ],
    dependencies : [
      plugin_deps,
    ],
    link_with : [
      fwupd,
      fwupdplugin,
    ],
    install : true,
    install_dir : installed_test_bindir,
  )
  test('ata-self-test', e, env : testdatadirs)  # added to installed-tests
endif
