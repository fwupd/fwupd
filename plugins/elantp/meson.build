if gudev.found()
cargs = ['-DG_LOG_DOMAIN="FuPluginElantp"']

plugin_quirks += files('elantp.quirk')

shared_module('fu_plugin_elantp',
  fu_hash,
  sources: [
    'fu-plugin-elantp.c',
    'fu-elantp-firmware.c',   # fuzzing
    'fu-elantp-hid-device.c',
    'fu-elantp-i2c-device.c',
  ],
  include_directories: [
    root_incdir,
    fwupd_incdir,
    fwupdplugin_incdir,
  ],
  install: true,
  install_dir: plugin_dir,
  c_args: [
    cargs,
    '-DLOCALSTATEDIR="' + localstatedir + '"',
  ],
  link_with: [
    fwupd,
    fwupdplugin,
  ],
  dependencies: [
    plugin_deps,
  ],
)
endif

if get_option('tests')
  install_data(['tests/elantp.builder.xml'],
    install_dir: join_paths(installed_test_datadir, 'tests'))
  env = environment()
  env.set('G_TEST_SRCDIR', meson.current_source_dir())
  env.set('G_TEST_BUILDDIR', meson.current_build_dir())
  e = executable(
    'elantp-self-test',
    fu_hash,
    sources: [
      'fu-self-test.c',
      'fu-elantp-firmware.c',
    ],
    include_directories: [
      root_incdir,
      fwupd_incdir,
      fwupdplugin_incdir,
    ],
    dependencies: [
      plugin_deps,
    ],
    link_with: [
      fwupd,
      fwupdplugin,
    ],
    install: true,
    install_rpath: plugin_dir,
    install_dir: installed_test_bindir,
  )
  test('elantp-self-test', e, env: env)
endif
