if hsi
cargs = ['-DG_LOG_DOMAIN="FuPluginLinuxSwap"']

plugin_builtin_linux_swap = static_library('fu_plugin_linux_swap',
  sources: [
    'fu-linux-swap-plugin.c',
    'fu-linux-swap.c',
  ],
  include_directories: plugin_incdirs,
  link_with: plugin_libs,
  c_args: cargs,
  dependencies: plugin_deps,
)
plugin_builtins += plugin_builtin_linux_swap

if get_option('tests')
  env = environment()
  env.set('G_TEST_SRCDIR', meson.current_source_dir())
  env.set('G_TEST_BUILDDIR', meson.current_build_dir())
  e = executable(
    'linux-swap-self-test',
    sources: [
      'fu-self-test.c',
    ],
    include_directories: plugin_incdirs,
    dependencies: plugin_deps,
    link_with: [
      plugin_libs,
      plugin_builtin_linux_swap,
    ],
    install: true,
    install_rpath: libdir_pkg,
    install_dir: installed_test_bindir,
    c_args: [
      '-DSRCDIR="' + meson.current_source_dir() + '"',
    ],
  )
  test('linux-swap-self-test', e, env: env)  # added to installed-tests
endif
endif
